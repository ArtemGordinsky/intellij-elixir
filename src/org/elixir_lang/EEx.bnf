{
  // CANNOT be called `Parser` because `
  parserClass="org.elixir_lang.eex.Parser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="EEx"
  psiImplClassSuffix="Impl"
  psiPackage="org.elixir_lang.eex.psi"
  psiImplPackage="org.elixir_lang.eex.psi.impl"

  elementTypeHolderClass="org.elixir_lang.eex.psi.Types"
  elementTypeClass="org.elixir_lang.eex.psi.ElementType"
  tokenTypeClass="org.elixir_lang.eex.psi.TokenType"

  tokens = [
    CLOSING = "%>"
    COMMENT = "Comment"
    DATA = "Data"
    EQUALS_MARKER = "="
    ELIXIR = "Elixir"
    FORWARD_SLASH_MARKER = "/"
    OPENING = "<%"
    OPENING_COMMENT = "<%#"
    OPENING_QUOTATION = "<%%"
    PIPE_MARKER = "|"
    QUOTATION = "Quotation"
  ]
}

private eexFile ::= DATA? (tags DATA?)?
private tags ::= tag (DATA? tag)*
private tag ::= elixirTag | commentTag | quotationTag

commentTag ::= OPENING_COMMENT COMMENT? CLOSING
elixirTag ::= OPENING marker? ELIXIR? CLOSING
marker ::= EQUALS_MARKER | FORWARD_SLASH_MARKER | PIPE_MARKER
quotationTag ::= OPENING_QUOTATION QUOTATION? CLOSING
